package workflows

import gwf.util.task.GenericAction
import gwf.util.task.sql.ExecuteSql
import gwf.workflows.data.JaxbXmlData
import gwf.workflows.data.JsonData
import gwf.workflows.data.XmlData
import gwf.workflows.data.YamlData
import gwf.workflows.sample.bean.SampleLocal
import org.jdbi.v3.core.statement.SqlLogger
import org.jdbi.v3.core.statement.StatementContext

logger.info 'Workflow found in workflows'

executor {
    properties.TEST = 'Blubb'
}

env.BLUBB = "abc"

tasks {

    task(GenericAction) {
        name = "task 1"
        logger.info "$name configured"

        action {
            url "someFile.txt" withInputStream {
                it.eachLine {
                    line -> logger.info line
                }
            }
        }

        action {
            logger.info load(String, "data/data.txt")
        }

        action {
            logger.info load(JsonData, "data/data.json").toString()
        }

        action {
            logger.info load(XmlData, "data/data.xml").toString()
        }

        action {
            logger.info load(JaxbXmlData, "data/data.jaxb.xml").toString()
        }

        action {
            logger.info load(YamlData, "data/data.yaml").toString()
        }
    }

    task("task 2", GenericAction) {

        action {
            logger.info "$name first action"
        }

        action {
            logger.info "$name second action"
            env.test = "property added by $name"
        }

        logger.info "$name configured"
    }
}

inline "sub/inlineWorkflow.wfl"

tasks {
    task("sql task", ExecuteSql) {

        jdbi {
            setSqlLogger(new SqlLogger() {
                @Override
                void logAfterExecution(StatementContext context) {
                    logger.info "Custom jdbi execute: ${context.renderedSql}"
                }
            })
        }

        sql {
            /
                create table if not exists WF_USERS (
                    NAME varchar(255),
                    ADDRESS varchar(255)
                )
            /
        }

        sql {
            /
                insert into WF_USERS
                    (NAME, ADDRESS)
                    values ('Hubert', 'Hinterdupfing'),
                        ('Darth Vader', 'Deathstar')
            /
        }

        selectBean(WfUser) {

            bind {
                name = 'Hubert'
            }

            define {
                table = 'WF_USERS'
            }

            "select NAME, ADDRESS from <table> where NAME = :name"
        }.then {
            it.each {
                user -> logger.info "selected $user.name from $user.address"
            }
        }

        logger.info "$name configured"
    }

    def bean = cdi SampleLocal

    task("print env", GenericAction) {
        logger.info "env (configuration): $env"
        action {
            logger.info "env (runtime): $env"
        }

        action {
            logger.info bean.sayHello()
        }
    }
}

class WfUser {
    String name
    String address
}
